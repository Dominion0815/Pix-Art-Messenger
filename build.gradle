// Top-level build file where you can add configuration options common to all
// sub-projects/modules.
buildscript {
		repositories {
				jcenter()
				mavenCentral()
		}
		dependencies {
				classpath 'com.android.tools.build:gradle:2.1.3'
		}
}

apply plugin: 'com.android.application'

repositories {
		jcenter()
		mavenCentral()
}

allprojects {
		repositories {
				maven { url "https://jitpack.io" }
		}
}

configurations {
		playstoreCompile
}

dependencies {
	compile project(':libs:MemorizingTrustManager')
	compile project(':libs:audiowife')
	compile 'org.sufficientlysecure:openpgp-api:10.0'
	compile 'com.soundcloud.android:android-crop:1.0.1@aar'
	compile 'com.android.support:support-v13:24.2.0'
	compile 'org.bouncycastle:bcprov-jdk15on:1.52'
	compile 'org.bouncycastle:bcmail-jdk15on:1.52'
	compile 'org.jitsi:org.otr4j:0.22'
	compile 'org.gnu.inet:libidn:1.15'
	compile 'com.google.zxing:core:3.2.1'
	compile 'com.google.zxing:android-integration:3.2.1'
	compile 'de.measite.minidns:minidns:0.1.7'
	compile 'de.timroes.android:EnhancedListView:0.3.4'
	compile 'me.leolin:ShortcutBadger:1.1.4@aar'
	compile 'com.kyleduo.switchbutton:library:1.2.8'
	compile 'org.whispersystems:axolotl-android:1.4.3'
	compile 'com.makeramen:roundedimageview:2.2.1'
	compile 'jetty:javax.servlet:5.1.12'
	compile 'com.google.code.gson:gson:2.4'
	compile 'org.jbundle.util.osgi.wrapped:org.jbundle.util.osgi.wrapped.org.apache.http.client:4.1.2'
	compile 'com.android.support:appcompat-v7:24.2.0'
	compile 'com.android.support:multidex:1.0.1'
	compile 'com.github.bumptech.glide:glide:3.7.0'
	compile 'com.github.chrisbanes:PhotoView:1.3.0'
	compile 'com.github.rtoshiro.fullscreenvideoview:fullscreenvideoview:1.1.1'
	compile 'com.google.android.gms:play-services-location:9.4.0'
	compile 'com.google.android.gms:play-services-maps:9.4.0'
	compile 'pub.devrel:easypermissions:0.1.9'
    compile "com.wefika:flowlayout:0.4.1"
    compile files('libs/aspectjrt-1.7.3.jar')
    compile files('libs/isoparser-1.0.6.jar')
	playstoreCompile 'com.google.android.gms:play-services-gcm:9.4.0'
}

ext {
		travisBuild = System.getenv("TRAVIS") == "true"
		// allows for -Dpre-dex=false to be set
		preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
}

android {

	compileSdkVersion 24
	buildToolsVersion "23.0.3"

	defaultConfig {
		minSdkVersion 14
		targetSdkVersion 24
		versionCode 163
		versionName "1.14.0"
		archivesBaseName += "-$versionName"
		applicationId "de.pixart.messenger"
		multiDexEnabled true
	}

	dexOptions {
		// Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
		preDexLibraries = preDexEnabled && !travisBuild
		javaMaxHeapSize "4g"
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	 productFlavors {
		playstore
		free
	}
	if (project.hasProperty('mStoreFile') &&
			project.hasProperty('mStorePassword') &&
			project.hasProperty('mKeyAlias') &&
			project.hasProperty('mKeyPassword')) {
		signingConfigs {
			release {
				storeFile file(mStoreFile)
					storePassword mStorePassword
					keyAlias mKeyAlias
					keyPassword mKeyPassword
			}
		}
		buildTypes.release.signingConfig = signingConfigs.release
	} else {
		buildTypes.release.signingConfig = null
	}

    sourceSets.main {
        jniLibs.srcDir 'libs'
        jni.srcDirs = [] //disable automatic ndk-build call
    }

	lintOptions {
		disable 'ExtraTranslation', 'MissingTranslation', 'InvalidPackage', 'MissingQuantity', 'AppCompatResource'
	}

	subprojects {

		afterEvaluate {
			if (getPlugins().hasPlugin('android') ||
					getPlugins().hasPlugin('android-library')) {

				configure(android.lintOptions) {
					disable 'AndroidGradlePluginVersion', 'MissingTranslation'
				}
			}

		}
	}

	packagingOptions {
		exclude 'META-INF/BCKEY.DSA'
		exclude 'META-INF/BCKEY.SF'
	}
}
